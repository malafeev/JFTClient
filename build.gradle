apply plugin: 'java'
apply plugin: 'application'

ext {
    userHome = System.getProperty("user.home")
}

applicationName = applicationName.toLowerCase()
archivesBaseName = applicationName
version = "0.3"
mainClassName = "org.jftclient.JFTClient"

jar {
    baseName = 'jftclient'
    version = "$version"
}

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
    maven {
        url 'http://www.sparetimelabs.com/maven2'
    }
}

configurations {
    all*.exclude group: "commons-logging", module: "commons-logging"
}

dependencies {
    compile 'org.controlsfx:controlsfx:8.20.8'

    compile name: 'pty4j-0.3'

    compile 'com.sparetimelabs:purejavacomm:0.0.22'
    compile 'net.java.dev.jna:jna:4.1.0'
    compile 'net.java.dev.jna:jna-platform:4.1.0'

    compile 'commons-io:commons-io:2.4'

    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'org.slf4j:jcl-over-slf4j:1.7.7'
    compile 'org.slf4j:log4j-over-slf4j:1.7.7'

    compile 'com.jcraft:jsch:0.1.51'

    compile 'com.google.guava:guava:18.0'

    compile 'org.springframework:spring-context:4.1.2.RELEASE'
    compile 'org.springframework:spring-orm:4.1.2.RELEASE'
    compile 'org.springframework.data:spring-data-jpa:1.7.1.RELEASE'

    compile 'org.hibernate:hibernate-entitymanager:4.3.7.Final'
    compile 'com.h2database:h2:1.4.182'

    //Test:
    testCompile 'org.testng:testng:6.8.8'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

test {
    useTestNG()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
        minGranularity = 3
    }
}

task copyFiles(type: Copy) {
    from "lib/libpty"
    into "build/install/$applicationName/lib/"
    //include('**/*')
}

distZip {
    into("$applicationName-$version") {
        from 'README'
        from 'LICENSE'
    }
}

applicationDistribution.from("lib/libpty") {
    into "lib/libpty"
}

task install {
    description = 'Build app and copy to home'
    doLast {
        exec {
            commandLine "rm";
            args "-rf", "${userHome}/${applicationName}"
        }
        exec {
            commandLine "cp";
            args "-rf", "build/install/${applicationName}", "${userHome}"
        }
    }
}

install.dependsOn clean, test, installApp
