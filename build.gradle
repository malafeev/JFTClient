apply plugin: 'java'
apply plugin: 'application'

ext {
    userHome = System.getProperty("user.home")
}

applicationName = applicationName.toLowerCase()
archivesBaseName = applicationName
version = "0.1"
mainClassName = "org.jftclient.JFTClient"

jar {
    baseName = 'jftclient'
    version = "$version"
}

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    all*.exclude group: "commons-logging", module: "commons-logging"
}

dependencies {
    compile 'commons-io:commons-io:2.4'

    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'org.slf4j:jcl-over-slf4j:1.7.6'

    compile 'com.jcraft:jsch:0.1.51'
    compile 'org.apache.sshd:sshd-core:0.11.0'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.50'

    compile 'com.google.guava:guava:17.0'

    compile 'org.springframework:spring-context:4.0.5.RELEASE'
    compile 'org.springframework:spring-orm:4.0.5.RELEASE'
    compile 'org.springframework.data:spring-data-jpa:1.6.0.RELEASE'

    compile 'org.hibernate:hibernate-entitymanager:4.3.5.Final'
    compile 'com.h2database:h2:1.4.178'

    //Test:
    testCompile 'org.testng:testng:6.8.8'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

test {
    useTestNG()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
        minGranularity = 3
    }
}

distZip {
    into("$applicationName-$version") {
        from 'README'
        from 'LICENSE'
    }
}

task install {
    description = 'Build app and copy to home'
    doLast {
        exec {
            commandLine "rm";
            args "-rf", "${userHome}/${applicationName}"
        }
        exec {
            commandLine "cp";
            args "-rf", "build/install/${applicationName}", "${userHome}"
        }
    }
}

install.dependsOn clean, test, installApp
